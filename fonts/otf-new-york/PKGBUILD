# Maintainer: Maxfield Wang <moposx01@gmail.com>
# shellcheck disable=SC2034,SC2148,SC2154
pkgname=otf-new-york
pkgver=16.0d2e2
pkgrel=2
pkgdesc='New York is a transitional serif typeface created by Apple Inc.'
arch=('any')
license=('custom')
url='https://developer.apple.com/fonts'
source=('https://devimages-cdn.apple.com/design/resources/download/NY-Font.dmg')
makedepends=('p7zip' 'texlive-bin')
sha256sums=('58058b5dbddb77eec84a0c0b10b41fc544bc7cd50c6cb49874da4197f91afde5')

prepare() {
    7z x NY-Font.dmg
    7z x NYFonts/NY\ Fonts.pkg
    7z x Payload\~
}

build() {
    # extract license from font file
    otfinfo -i Library/Fonts/NewYork.ttf | sed -n '/You may use/,/in writing./p' > LICENSE
    sed -i 's/License Description: //' LICENSE
}

package() {
    install -Dm644 -t "${pkgdir}/usr/share/fonts/${pkgname}" Library/Fonts/*.otf
    install -Dm644 -t "${pkgdir}/usr/share/fonts/${pkgname}" Library/Fonts/*.ttf

    # install license
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
