# Maintainer: Maxfield Wang <moposx01@gmail.com>
# shellcheck disable=SC2034,SC2148,SC2154
pkgname=otf-sf-mono
pkgver=16.0d2e1
pkgrel=2
pkgdesc='Monospaced variant of San Francisco Pro, supporting Latin, Greek, and Cyrillic scripts, including six weights in normal and italic style.'
arch=('any')
license=('custom')
url='https://developer.apple.com/fonts'
source=('https://devimages-cdn.apple.com/design/resources/download/SF-Mono.dmg')
makedepends=('p7zip')
sha256sums=('ec0518e310797d2f9cb924c18e3e7b661359f4fb653d1ad4315758ebcdb5ff11')

prepare() {
    7z x SF-Mono.dmg
    7z x SFMonoFonts/SF\ Mono\ Fonts.pkg
    7z x Payload\~
}

package() {
    install -Dm644 -t "${pkgdir}/usr/share/fonts/${pkgname}" Library/Fonts/*.otf
    
    # install license
    echo '(c) 2015-2021 Apple Inc. All rights reserved.' > LICENSE
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
